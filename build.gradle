import org.gradle.api.artifacts.maven.MavenDeployment

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'com.amazonaws:aws-java-sdk-s3:1.11.82'
    compile 'joda-time:joda-time:2.4'
}

repositories {
    mavenCentral()
}

def isReleaseVersion = !(version ==~ /(?i).*SNAPSHOT$/)
if (isReleaseVersion) {
    project.ext['upload.url'] = project.ext['upload.release.url']
} else {
    project.ext['upload.url'] = project.ext['upload.snapshot.url']
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from "$buildDir/docs/javadoc"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}
